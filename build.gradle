description "Solr running as Undertow.io servlet"

buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${version_kotlin}"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'eclipse'

mainClassName = "org.bremeld.solr.undertow.UndertowPackage"
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]

// set other parameters in SOLR_UNDERTOW_OPTS environment variable, for example:
// export SOLR_UNDERTOW_OPTS="-Xms15G -Xmx15G -XX:MaxPermSize=512m -XX:PermSize=256m -Dcom.sun.management.jmxremote -Dcom.sun.management.jmxremote.port=9901 -Dcom.sun.management.jmxremote.ssl=false -Dcom.sun.management.jmxremote.authenticate=false

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

tasks.withType(Tar){
    compression = Compression.GZIP
}


task distWithSolr(type: Tar, dependsOn: distTar) {
    into("${baseName}-${version}-with-solr-${version_solr}")
    from(project.file("src/dist"))
    into("lib") {
        from(jar)
        from(project.configurations.runtime)
    }
    into("bin") {
        from(startScripts)
        fileMode = 0755
    }
    from("example") {
        into("example")
        exclude("**/.ignore")
        exclude("solr-temp/4*")
        exclude("solr-logs/solr.log")
        exclude("solr-wars/*.war")
        exclude("solr-wars/*.zip")
    }
    from("example/solr-wars/solr-${version_solr}.war") {
        into("example/solr-wars")
    }
    from("example/solr-wars/solr-${version_solr}.zip") {
        into("example/solr-wars")
    }
    classifier = "with-solr-${version_solr}"
}

// TODO: make this copyspec work as shared between dist TAR and ZIP, and figure out the application plugin change with new distribution plugin base
task distZipWithSolr(type: Zip, dependsOn: distZip) {
    into("${baseName}-${version}-with-solr-${version_solr}")
    from(project.file("src/dist"))
    into("lib") {
        from(jar)
        from(project.configurations.runtime)
    }
    into("bin") {
        from(startScripts)
        fileMode = 0755
    }
    from("example") {
        into("example")
        exclude("**/.ignore")
        exclude("solr-temp/4*")
        exclude("solr-logs/solr.log")
        exclude("solr-wars/*.war")
        exclude("solr-wars/*.zip")
    }
    from("example/solr-wars/solr-${version_solr}.war") {
        into("example/solr-wars")
    }
    from("example/solr-wars/solr-${version_solr}.zip") {
        into("example/solr-wars")
    }
    classifier = "with-solr-${version_solr}"
}

task(distAll)
distAll.dependsOn clean
distAll.dependsOn test
distAll.dependsOn distWithSolr
distAll.dependsOn distZipWithSolr
distAll.dependsOn installDist

distributions {
    main {
        contents {
            from("example") {
                into("example")
                exclude("**/.ignore")
                exclude("solr-temp/4*")
                exclude("solr-logs/solr.log")
                exclude("solr-wars/*.war")
                exclude("solr-wars/*.zip")
            }
        }
    }
}

repositories {
    jcenter()
    maven {
        url 'http://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    compile "io.undertow:undertow-servlet:${version_undertow}"

    compile "com.typesafe:config:${version_typesafe_config}"

    compile "org.jetbrains.kotlin:kotlin-stdlib:${version_kotlin}"

    compile "org.slf4j:slf4j-api:${version_slf4j}"
    runtime "ch.qos.logback:logback-classic:${version_logback}"
    runtime "ch.qos.logback:logback-access:${version_logback}"
    runtime "org.slf4j:jcl-over-slf4j:${version_slf4j}"
    runtime "org.slf4j:jul-to-slf4j:${version_slf4j}"
    runtime "org.slf4j:log4j-over-slf4j:${version_slf4j}"

    testCompile "junit:junit:${version_junit}"
}
